buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'maven'

version = "1.7.10-1.0.4"
group= "net.orekyuu"
archivesBaseName = "rabbithouse"
project.ext.forgeVersion = "1.7.10-10.13.0.1180"

minecraft {
    version = project.ext.forgeVersion
    assetDir = "eclipse/assets"
}

dependencies {
    compile project(':RabbitHouseTool')
    compile 'com.google.code.gson:gson:2.2.4'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
}

javadoc {
    source += sourceSets.api.allSource
    options.encoding = "UTF-8"
    options.locale = 'ja_JP'
    project.configure(options) {
        memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        charSet = "UTF-8"
        docTitle = "Rabbit House"
        windowTitle = "Rabbit House"
        author = "true"
        use = "true"
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

jar {
    from sourceSets.main.output
}

build.dependsOn sourceJar, javadocJar, deobfJar

configurations {
    deployerJars
}

artifacts {
    archives sourceJar
    archives javadocJar
    archives deobfJar
}

task createBlock << {
    File blocksFile = file('src/main/resources/net/orekyuu/rabbithouse/block/Blocks.json')
    blocksFile.parentFile.mkdirs()
    if(blocksFile.createNewFile())
        println 'created Blocks.json.'
}

task readCvs << {
    forgeDir = project.getGradle().getGradleUserHomeDir().getAbsolutePath().replace('\\', '/') +
            '/caches/minecraft/net/minecraftforge/forge/'
    File fields = new File(forgeDir + forgeVersion + '/unpacked/conf/fields.csv')
    fields.eachLine{
        println it
    }
}

task initRabbitHouse << {
    println '======================'
    println 'Initialize RabbitHouse'
    println '======================'

    tasks.createBlock.execute()
}
[compileJava, compileTestJava].each{
    it.options.encoding = 'UTF-8'
    it.options.compilerArgs += ['-source', '1.7', '-target', '1.7']
}

uploadArchives {
    dependsOn 'reobf'
    repositories {
        mavenDeployer {
            repository(url: "file:${System.getProperty('user.home')}/public/.m2/repository")
        }
    }
}